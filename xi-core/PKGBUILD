# Maintainer: Philip Goto <philip.goto@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=xi-core
_project=xi-editor
pkgver=0.3.0
pkgrel=2
pkgdesc='A modern editor with a backend written in Rust'
arch=('x86_64' 'i686')
url="https://github.com/$_project/$_project"
license=('Apache')
makedepends=('cargo')
provides=('xi-editor')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e42998ad9b6d6df0f797afd005f9c1286aad24c30a77d0dd380730c248274b8d')

prepare() {
    cd "$_project-$pkgver/rust"
    cargo fetch --locked
}

build() {
    cd "$_project-$pkgver/rust"
    cargo build --locked --offline --release --all-features
}

check() {
    cd "$_project-$pkgver/rust"
    cargo test --locked --offline --release
}

package() {
    cd "$_project-$pkgver/rust"
    install -Dm0755 -t "$pkgdir/usr/bin" "target/release/$pkgname"
}
